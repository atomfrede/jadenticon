plugins {
    id 'pl.allegro.tech.build.axion-release' version '1.8.1'
    id 'com.github.ben-manes.versions' version '0.17.0'
    id "com.diffplug.gradle.spotless" version "3.7.0"
}

scmVersion {
    tag {
        prefix = ''
    }
    hooks {
        pre 'fileUpdate', [
            file: 'README.md', pattern: {v, c -> /<version>$v/}, replacement: {v, c -> "<version>$v"}
        ]
        pre 'fileUpdate', [
            file: 'README.md', pattern: {v, c -> /com.github.atomfrede:jadenticon:$v/}, replacement: {v, c -> "com.github.atomfrede:jadenticon:$v"}
        ]
        pre 'commit'
    }
}
ext.moduleName = 'com.github.atomfrede.jadenticon'

group 'com.github.atomfrede'

apply plugin: 'java'
apply plugin: 'com.github.ben-manes.versions'
apply from: 'gradle/codeQuality.gradle'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'commons-io', name: 'commons-io', version: '2.6'
    compile group: 'org.apache.xmlgraphics', name: 'batik-transcoder', version: '1.9.1'
    compile group: 'org.apache.xmlgraphics', name: 'xmlgraphics-commons', version: '2.2'
    compile group: 'org.apache.xmlgraphics', name: 'batik-codec', version: '1.9.1'
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.assertj', name: 'assertj-core', version: '3.8.0'
}

jar {
    inputs.property("moduleName", moduleName)
    manifest {
        attributes('Automatic-Module-Name': moduleName)
    }
}

project.version = scmVersion.version

check.dependsOn jacocoTestReport

task wrapper(type: Wrapper) {
    gradleVersion = '4.4'
}
