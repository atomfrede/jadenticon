plugins {
    id 'java'
    id 'jacoco'
    id 'maven'
    id 'pl.allegro.tech.build.axion-release' version '1.13.6'
    id "com.github.ben-manes.versions" version "0.41.0"
    id "com.diffplug.spotless" version "6.0.2"
    id "com.github.andygoossens.gradle-modernizer-plugin" version "1.6.1"
    id 'com.adarshr.test-logger' version '3.1.0'
}

ext {
    junitVersion = '5.8.2'
}

scmVersion {
    tag {
        prefix = ''
    }
    hooks {
        pre 'fileUpdate', [
            file: 'README.md', pattern: {v, c -> /<version>$v/}, replacement: {v, c -> "<version>$v"}
        ]
        pre 'fileUpdate', [
            file: 'README.md', pattern: {v, c -> /com.github.atomfrede:jadenticon:$v/}, replacement: {v, c -> "com.github.atomfrede:jadenticon:$v"}
        ]
        pre 'commit'
    }
}

spotless {
    format 'misc', {
        target '**/*.gradle', '**/*.md', '**/*.adoc', '**/.gitignore'
        targetExclude 'node_modules/**/*.*', ".gradle/**", "build/**/*.*", "target/**/*.*", "src/test/e2e/node_modules/**"
        trimTrailingWhitespace()
        indentWithSpaces()
        endWithNewline()
    }
}

test {
    include '**/*Test.*'
    exclude '**/*Spec.*', '**/*IT.*'
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}


java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

jacoco {
    toolVersion = "0.8.7"
}

ext.moduleName = 'com.github.atomfrede.jadenticon'

group 'com.github.atomfrede'


repositories {
    mavenCentral()
}

dependencies {
    implementation "commons-io:commons-io:2.11.0"
    implementation 'org.apache.xmlgraphics:batik-transcoder:1.14'
    implementation 'org.apache.xmlgraphics:xmlgraphics-commons:2.6'
    implementation 'org.apache.xmlgraphics:batik-codec:1.14'
    implementation 'org.openjdk.nashorn:nashorn-core:15.3'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.8.2'
    implementation 'org.assertj:assertj-core:3.22.0'
    testImplementation "com.tngtech.archunit:archunit-junit5-api:0.22.0"
    testRuntimeOnly "com.tngtech.archunit:archunit-junit5-engine:0.22.0"
}

jar {
    inputs.property("moduleName", moduleName)
    manifest {
        attributes('Automatic-Module-Name': moduleName)
    }
}

project.version = scmVersion.version

check.dependsOn jacocoTestReport
